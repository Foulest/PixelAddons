plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '1.1.0'
description = project.name

compileJava.options.encoding = 'UTF-8'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/central'
        name 'Sonatype Central'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
        name 'Sonatype Snapshots'
    }

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        name 'Spigot Snapshots'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
}

dependencies {
    // Spigot API - necessary for project
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT'

    // AtomDev - for accessing Minecraft classes; provided in libs
    // https://github.com/josephworks/AtomMC/releases/download/v2.1/AtomDev.jar
    compileOnly group: 'org.atom', name: 'AtomDev', version: '1.12.2'

    // CatServer - for listening to Forge events; provided in libs
    // https://catmc.org/download/universal
    compileOnly group: 'catserver', name: 'CatServer', version: '1.12.2'

    // Pixelmon - for listening to Pixelmon events; provided in libs
    // https://reforged.gg (Download 1.12.2)
    compileOnly group: 'com.pixelmonmod', name: 'Pixelmon', version: '1.12.2'

    // PixelHunt Remastered - for listening to PixelHunt events; provided in libs
    // https://pixelmonmod.com/wiki/PixelHunt_Remastered
    compileOnly group: 'com.envyful', name: 'PixelHuntRemastered', version: '3.0.2'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
}

tasks {
    jar {
        archiveFileName.set("${project.name}-${project.version}-default.jar")
    }

    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        enableRelocation = true
        relocationPrefix = "net.foulest.pixeladdons.shaded"
        minimize()
    }

    compileJava {
        dependsOn(clean)
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/plugin.yml') {
            expand(project.properties)
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "net.foulest.pixeladdons"
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
